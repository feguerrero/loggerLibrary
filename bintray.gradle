/*
 * Copyright 2020 CondorLabs
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.github.dcendents.android-maven'

apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 28
}

ext {
    libraryVersion = '0.0.1'

    bintrayRepo = 'LoggerLibrary'
    bintrayName = 'LoggerLibrary'

    publishedGroupId = 'co.condorlabs.loggerlibrary'
    libraryName = 'LoggerLibrary'
    artifact = 'LoggerLibrary'

    libraryDescription = 'Implement different log services with the same interface.'

    siteUrl = 'https://github.com/feguerrero/loggerLibrary'
    gitUrl = 'https://github.com/feguerrero/loggerLibrary.git'

    developerId = 'condorlabs'
    developerName = 'Felipe Guerrero'
    developerEmail = 'ferazo@condorlabs.io'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {

    Properties properties = new Properties()
    properties.load(project.rootProject.file('gradle.properties').newDataInputStream())

    user = properties.getProperty("bintray_user")
    key = properties.getProperty("bintray_apikey")
    configurations = ['archives']

    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}
